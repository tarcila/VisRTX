// Copyright 2026 NVIDIA Corporation
// SPDX-License-Identifier: Apache-2.0

#include "../math/color.h"
#include "tsd/algorithms/cuda/visualizeAOV.hpp"
// helium
#include <helium/helium_math.h>
// thrust
#include <thrust/execution_policy.h>
#include <thrust/for_each.h>
#include <thrust/iterator/counting_iterator.h>
// std
#include <algorithm>

namespace tsd::algorithms::cuda {

// ID /////////////////////////////////////////////////////////////////////////

void visualizeId(cudaStream_t stream,
    const uint32_t *id,
    uint32_t *color,
    uint32_t width,
    uint32_t height)
{
  thrust::for_each(thrust::cuda::par.on(stream),
      thrust::make_counting_iterator(0u),
      thrust::make_counting_iterator(width * height),
      [=] __device__(uint32_t i) {
        uint32_t v = id ? id[i] : ~0u;
        auto c = math::makeRandomColor(v);
        color[i] = helium::cvt_color_to_uint32({c, 1.f});
      });
}

void visualizeId(
    const uint32_t *id, uint32_t *color, uint32_t width, uint32_t height)
{
  visualizeId(cudaStream_t{0}, id, color, width, height);
}

// Depth ///////////////////////////////////////////////////////////////////////

void visualizeDepth(cudaStream_t stream,
    const float *depth,
    uint32_t *color,
    float minDepth,
    float maxDepth,
    uint32_t width,
    uint32_t height)
{
  thrust::for_each(thrust::cuda::par.on(stream),
      thrust::make_counting_iterator(0u),
      thrust::make_counting_iterator(width * height),
      [=] __device__(uint32_t i) {
        const float d = depth ? depth[i] : 0.f;
        const float range = maxDepth - minDepth;
        const float v =
            range > 0.f ? fminf(fmaxf((d - minDepth) / range, 0.f), 1.f) : 0.f;
        color[i] = helium::cvt_color_to_uint32({tsd::math::float3(v), 1.f});
      });
}

void visualizeDepth(const float *depth,
    uint32_t *color,
    float minDepth,
    float maxDepth,
    uint32_t width,
    uint32_t height)
{
  visualizeDepth(
      cudaStream_t{0}, depth, color, minDepth, maxDepth, width, height);
}

// Albedo //////////////////////////////////////////////////////////////////////

void visualizeAlbedo(cudaStream_t stream,
    const tsd::math::float3 *albedo,
    uint32_t *color,
    uint32_t width,
    uint32_t height)
{
  thrust::for_each(thrust::cuda::par.on(stream),
      thrust::make_counting_iterator(0u),
      thrust::make_counting_iterator(width * height),
      [=] __device__(uint32_t i) {
        const auto a = albedo ? albedo[i] : tsd::math::float3(0.f);
        color[i] = helium::cvt_color_to_uint32_srgb({a, 1.f});
      });
}

void visualizeAlbedo(const tsd::math::float3 *albedo,
    uint32_t *color,
    uint32_t width,
    uint32_t height)
{
  visualizeAlbedo(cudaStream_t{0}, albedo, color, width, height);
}

// Normal //////////////////////////////////////////////////////////////////////

void visualizeNormal(cudaStream_t stream,
    const tsd::math::float3 *normal,
    uint32_t *color,
    uint32_t width,
    uint32_t height)
{
  thrust::for_each(thrust::cuda::par.on(stream),
      thrust::make_counting_iterator(0u),
      thrust::make_counting_iterator(width * height),
      [=] __device__(uint32_t i) {
        const auto n = normal ? normal[i] : tsd::math::float3(0.f);
        const auto visualNormal = (n + 1.f) * 0.5f;
        color[i] = helium::cvt_color_to_uint32({visualNormal, 1.f});
      });
}

void visualizeNormal(const tsd::math::float3 *normal,
    uint32_t *color,
    uint32_t width,
    uint32_t height)
{
  visualizeNormal(cudaStream_t{0}, normal, color, width, height);
}

// Edges ///////////////////////////////////////////////////////////////////////

void visualizeEdges(cudaStream_t stream,
    const uint32_t *objectId,
    uint32_t *color,
    bool invert,
    uint32_t width,
    uint32_t height)
{
  thrust::for_each(thrust::cuda::par.on(stream),
      thrust::make_counting_iterator(0u),
      thrust::make_counting_iterator(width * height),
      [=] __device__(uint32_t i) {
        uint32_t y = i / width;
        uint32_t x = i % width;

        uint32_t centerID = objectId ? objectId[i] : ~0u;

        if (centerID == ~0u) {
          color[i] = helium::cvt_color_to_uint32({tsd::math::float3(0.f), 1.f});
          return;
        }

        bool isEdge = false;

        for (int dy = -1; dy <= 1 && !isEdge; ++dy) {
          for (int dx = -1; dx <= 1 && !isEdge; ++dx) {
            if (dx == 0 && dy == 0)
              continue;

            int nx = static_cast<int>(x) + dx;
            int ny = static_cast<int>(y) + dy;

            if (nx >= 0 && nx < static_cast<int>(width) && ny >= 0
                && ny < static_cast<int>(height)) {
              size_t neighborIdx =
                  static_cast<size_t>(nx) + static_cast<size_t>(ny) * width;
              uint32_t neighborID = objectId ? objectId[neighborIdx] : ~0u;

              if (centerID != neighborID)
                isEdge = true;
            }
          }
        }

        float edgeValue = isEdge ? 1.f : 0.f;
        if (invert)
          edgeValue = 1.f - edgeValue;

        color[i] =
            helium::cvt_color_to_uint32({tsd::math::float3(edgeValue), 1.f});
      });
}

void visualizeEdges(const uint32_t *objectId,
    uint32_t *color,
    bool invert,
    uint32_t width,
    uint32_t height)
{
  visualizeEdges(cudaStream_t{0}, objectId, color, invert, width, height);
}

} // namespace tsd::algorithms::cuda
